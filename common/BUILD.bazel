load("@rules_distroless//distroless:defs.bzl", "cacerts", "group", "home", "locale", "os_release", "passwd")
load("//:checksums.bzl", "ARCHITECTURES", "VERSIONS")
load("//base:distro.bzl", "DISTROS")
load(":variables.bzl", "NOBODY", "NONROOT", "OS_RELEASE", "ROOT", "quote")

package(default_visibility = ["//visibility:public"])

[
    os_release(
        name = "os_release_%s" % dist,
        content = {
            key: quote(value.format(
                CODENAME = codename,
                VERSION = version,
            ))
            for (key, value) in OS_RELEASE.items()
        },
    )
    for (dist, codename, version) in VERSIONS
]

[
    locale(
        name = "locale_%s_%s" % (distro, arch),
        # From Debian 12 the C.UTF-8 locale has been renamed into C.utf8:
        # https://metadata.ftp-master.debian.org/changelogs//main/g/glibc/glibc_2.36-9+deb12u1_changelog
        # >   * debian/debhelper.in/libc-bin.install, debian/rules.d/build.mk,
        # >     debian/rules: rename the C.UTF-8 locale into C.utf8 to match upstream
        # >     naming.
        charset = "C.UTF-8" if distro == "debian11" else "C.utf8",
        package = "@%s_%s_libc-bin//:data" % (arch, distro),
    )
    for arch in ARCHITECTURES
    for distro in DISTROS
]

[
    cacerts(
        name = "cacerts_%s_%s" % (distro, arch),
        package = "@%s_%s_ca-certificates//:data" % (arch, distro),
    )
    for arch in ARCHITECTURES
    for distro in DISTROS
]

# create /etc/group with the root, tty, and staff groups
group(
    name = "group",
    groups = [
        {
            "name": "root",  # root_group
            "gid": ROOT,
            "password": "x",
            "users": [],
        },
        {
            "name": "nobody",  # nobody_group
            "gid": NOBODY,
            "password": "x",
            "users": [],
        },
        {
            "name": "nonroot",  # nonroot_group
            "gid": NONROOT,
            "password": "x",
            "users": [],
        },
        {
            "name": "tty",  # tty_group
            "gid": 5,
            "password": "x",
            "users": [],
        },
        {
            "name": "staff",  # staff_group
            "gid": 50,
            "password": "x",
            "users": [],
        },
    ],
)

passwd(
    name = "passwd",
    passwds = [
        {
            "gecos": ["nobody"],
            "gid": NOBODY,
            "home": "/nonexistent",
            "shell": "/sbin/nologin",
            "uid": NOBODY,
            "password": "x",
            "username": "nobody",
        },
        {
            "gecos": ["root"],
            "gid": ROOT,
            "shell": "/sbin/nologin",
            "home": "/root",
            "uid": ROOT,
            "password": "x",
            "username": "root",
        },
        {
            "gecos": ["nonroot"],
            "gid": NONROOT,
            "home": "/home/nonroot",
            "shell": "/sbin/nologin",
            "uid": NONROOT,
            "password": "x",
            "username": "nonroot",
        },
    ],
)

home(
    name = "home",
    dirs = [
        {
            "home": "./root",
            "uid": ROOT,
            "gid": ROOT,
        },
        {
            "home": "./home/nonroot",
            "uid": NONROOT,
            "gid": NONROOT,
        },
    ],
)
