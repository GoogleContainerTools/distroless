BUILD_TMPL = """\
# GENERATED BY debian_archive.bzl
load("@distroless//pkg:dpkg_status.bzl", "dpkg_status")
load("@distroless//pkg:debian_spdx.bzl", "debian_spdx")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

filegroup(
	name = "control",
	srcs = ["control.tar.xz"],
	visibility = ["//visibility:public"]
)
filegroup(
	name = "data",
	srcs = ["data.tar.xz"],
	visibility = ["//visibility:public"]
)

dpkg_status(
    name = "dpkg_status",
    control = "control.tar.xz",
    package_name = "{package_name}"
)

pkg_tar(
    name = "tar",
	deps = [":dpkg_status", ":data"]
)

debian_spdx(
    name = "{name}",
    visibility = ["//visibility:public"],
    control = "control.tar.xz",
    tar = ":tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)
"""

def _impl(rctx):
    rctx.report_progress('test')
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = "deb"
    )
    rctx.file(
        "BUILD.bazel", 
        content = BUILD_TMPL.format(
            name = rctx.attr.name,
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        )
    )


debian_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "package_name": attr.string(mandatory = True),
        "urls": attr.string_list(mandatory=True),
        "sha256": attr.string(mandatory=True),
    }
)