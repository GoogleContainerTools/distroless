"debian dependencies"

REPOS = [
    "trixie",
    "trixie_java",
    "bookworm",
    "bookworm_java",
    "bookworm_python",
]

apt = use_extension("@rules_distroless//apt:extensions.bzl", "apt")

[
    apt.install(
        name = repo,
        lock = "//private/repos/deb:{}.lock.json".format(repo),
        manifest = "//private/repos/deb:{}.yaml".format(repo),
        package_template = "//private/repos/deb:package.BUILD.tmpl",
        resolve_transitive = False,
        ### TODO: trixie should add merged-usr support. It's added to rules_distroless but not released yet.
        ### Once released, bump the version and enable the following line.
        # mergedusr = repo == "trixie",
    )
    for repo in REPOS
]

use_repo(apt, "bookworm", "bookworm_java", "bookworm_python", "trixie", "trixie_java")

### VERSIONS HUB REPO ###
version = use_extension("//private/extensions:version.bzl", "version")

[
    version.from_lock(
        lock = "//private/repos/deb:{}.lock.json".format(repo),
        repo_name = repo,
    )
    for repo in REPOS
]

use_repo(version, "versions")
