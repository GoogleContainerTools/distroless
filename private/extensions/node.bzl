"node"

BUILD_TMPL = """\
# GENERATED BY node_archive.bzl
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        [
            "output/bin/node",
            "output/LICENSE",
        ],
    ),
    package_dir = "/nodejs",
    strip_prefix = "external/{canonical_name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    rctx.report_progress("Fetching {}".format(rctx.attr.package_name))
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = rctx.attr.type,
        stripPrefix = rctx.attr.strip_prefix,
        output = "output",
    )
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
            "{{SHA256}}": rctx.attr.sha256,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            canonical_name = rctx.attr.name,
            name = rctx.attr.name.split("~")[-1],
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

node_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".tar.gz"),
        "strip_prefix": attr.string(),
        "package_name": attr.string(default = "nodejs"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        # control is only used to populate the sbom, see https://github.com/GoogleContainerTools/distroless/issues/1373
        # for why writing debian control files to the image is incompatible with scanners.
        "control": attr.label(),
    },
)

def _node_impl(module_ctx):
    mod = module_ctx.modules[0]

    if len(module_ctx.modules) > 1:
        fail("node.archive should be called only once")
    if not mod.is_root:
        fail("node.archive should be called from root module only.")

    # Node (https://nodejs.org/en/about/releases/)
    # Follow Node's maintainence schedule and support all LTS versions that are not end of life

    node_archive(
        name = "nodejs20_amd64",
        sha256 = "eec2c7b9c6ac72e42885a42edfc0503c0e4ee455f855c4a17a6cbcf026656dd5",
        strip_prefix = "node-v20.19.2-linux-x64/",
        urls = ["https://nodejs.org/dist/v20.19.2/node-v20.19.2-linux-x64.tar.gz"],
        version = "20.19.2",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm64",
        sha256 = "24c3090d4e8c3667cd57482263291ca4f562c2e0773d5e618a0c6ba32d21b39f",
        strip_prefix = "node-v20.19.2-linux-arm64/",
        urls = ["https://nodejs.org/dist/v20.19.2/node-v20.19.2-linux-arm64.tar.gz"],
        version = "20.19.2",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm",
        sha256 = "aed2de774737d76a81254cbcd12ae5e9239f24efec0456b9608f150d27d58fe7",
        strip_prefix = "node-v20.19.2-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v20.19.2/node-v20.19.2-linux-armv7l.tar.gz"],
        version = "20.19.2",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_ppc64le",
        sha256 = "9fced2e270fb0879709f7fa19b358551244b2d6c8cbf39d6a860221dcc12181b",
        strip_prefix = "node-v20.19.2-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v20.19.2/node-v20.19.2-linux-ppc64le.tar.gz"],
        version = "20.19.2",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_s390x",
        sha256 = "94023e739c98ce55a057fbf5e86589253458e92af1990a2712dab8c6421cfe2f",
        strip_prefix = "node-v20.19.2-linux-s390x/",
        urls = ["https://nodejs.org/dist/v20.19.2/node-v20.19.2-linux-s390x.tar.gz"],
        version = "20.19.2",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_amd64",
        sha256 = "f4b8eec683708acb1a2a73c7182ba2de5466a5dd5f705934a0830903df28821c",
        strip_prefix = "node-v22.15.1-linux-x64/",
        urls = ["https://nodejs.org/dist/v22.15.1/node-v22.15.1-linux-x64.tar.gz"],
        version = "22.15.1",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm64",
        sha256 = "eb3f232b83dfe83397b98395ec77a973e888e8959c978b3e4eeb551b8845b74f",
        strip_prefix = "node-v22.15.1-linux-arm64/",
        urls = ["https://nodejs.org/dist/v22.15.1/node-v22.15.1-linux-arm64.tar.gz"],
        version = "22.15.1",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm",
        sha256 = "346426e2bca62c98fb12213c39e80b0e349d7620238f74b7208d12e18fde87fd",
        strip_prefix = "node-v22.15.1-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v22.15.1/node-v22.15.1-linux-armv7l.tar.gz"],
        version = "22.15.1",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_ppc64le",
        sha256 = "156518af6901fb134ddde5f4b7ee39e047c4154480c3dd93030912a5d3f87097",
        strip_prefix = "node-v22.15.1-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v22.15.1/node-v22.15.1-linux-ppc64le.tar.gz"],
        version = "22.15.1",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_s390x",
        sha256 = "6b5a9615b66e553189578a4be33099f70add96c3b00bf38b7dcd91825ea1672f",
        strip_prefix = "node-v22.15.1-linux-s390x/",
        urls = ["https://nodejs.org/dist/v22.15.1/node-v22.15.1-linux-s390x.tar.gz"],
        version = "22.15.1",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_amd64",
        sha256 = "58239e217440acffb3889954f1dc0977f46048c54f226e446280fc8feb8c5ab9",
        strip_prefix = "node-v24.0.1-linux-x64/",
        urls = ["https://nodejs.org/dist/v24.0.1/node-v24.0.1-linux-x64.tar.gz"],
        version = "24.0.1",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_arm64",
        sha256 = "45a5ffeff5eae91e64f19575072f0241ff71da49c0cdf69fad360323c587a5bd",
        strip_prefix = "node-v24.0.1-linux-arm64/",
        urls = ["https://nodejs.org/dist/v24.0.1/node-v24.0.1-linux-arm64.tar.gz"],
        version = "24.0.1",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_ppc64le",
        sha256 = "66d74261b98d0aec8bec6b980e0f49c5b95543e4b2d365394cf96c7294b9d5a6",
        strip_prefix = "node-v24.0.1-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v24.0.1/node-v24.0.1-linux-ppc64le.tar.gz"],
        version = "24.0.1",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_s390x",
        sha256 = "a65aac6ec6324c0b81a68bf8a701cb2cb8654be1422866908b42dba680c9368d",
        strip_prefix = "node-v24.0.1-linux-s390x/",
        urls = ["https://nodejs.org/dist/v24.0.1/node-v24.0.1-linux-s390x.tar.gz"],
        version = "24.0.1",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = [
            "nodejs20_amd64",
            "nodejs20_arm64",
            "nodejs20_arm",
            "nodejs20_ppc64le",
            "nodejs20_s390x",
            "nodejs22_amd64",
            "nodejs22_arm64",
            "nodejs22_arm",
            "nodejs22_ppc64le",
            "nodejs22_s390x",
            "nodejs24_amd64",
            "nodejs24_arm64",
            "nodejs24_ppc64le",
            "nodejs24_s390x",
        ],
        root_module_direct_dev_deps = [],
    )

_archive = tag_class(attrs = {})

node = module_extension(
    implementation = _node_impl,
    tag_classes = {
        "archive": _archive,
    },
)
