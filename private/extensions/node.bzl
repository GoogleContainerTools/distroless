"node"

BUILD_TMPL = """\
# GENERATED BY node_archive.bzl
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        [
            "output/bin/node",
            "output/LICENSE",
        ],
    ),
    package_dir = "/nodejs",
    strip_prefix = "external/{canonical_name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    rctx.report_progress("Fetching {}".format(rctx.attr.package_name))
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = rctx.attr.type,
        stripPrefix = rctx.attr.strip_prefix,
        output = "output",
    )
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
            "{{SHA256}}": rctx.attr.sha256,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            canonical_name = rctx.attr.name,
            name = rctx.attr.name.split("~")[-1],
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

node_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".tar.gz"),
        "strip_prefix": attr.string(),
        "package_name": attr.string(default = "nodejs"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        # control is only used to populate the sbom, see https://github.com/GoogleContainerTools/distroless/issues/1373
        # for why writing debian control files to the image is incompatible with scanners.
        "control": attr.label(),
    },
)

def _node_impl(module_ctx):
    mod = module_ctx.modules[0]

    if len(module_ctx.modules) > 1:
        fail("node.archive should be called only once")
    if not mod.is_root:
        fail("node.archive should be called from root module only.")

    # Node (https://nodejs.org/en/about/releases/)
    # Follow Node's maintainence schedule and support all LTS versions that are not end of life

    node_archive(
        name = "nodejs20_amd64",
        sha256 = "24344f9f03e3b388a901f8fb5d98e627e6cf74ce44aaf5a0fd6c4b39e4f2adc8",
        strip_prefix = "node-v20.19.6-linux-x64/",
        urls = ["https://nodejs.org/dist/v20.19.6/node-v20.19.6-linux-x64.tar.gz"],
        version = "20.19.6",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm64",
        sha256 = "a332bb8b108d7aab2f2e2736293e4ab41638cf6f3b3f49a756b8f63469024fa5",
        strip_prefix = "node-v20.19.6-linux-arm64/",
        urls = ["https://nodejs.org/dist/v20.19.6/node-v20.19.6-linux-arm64.tar.gz"],
        version = "20.19.6",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm",
        sha256 = "5ac7beb1d4237478bba571619454ee9724602f75012c46a4e6636a45725e749f",
        strip_prefix = "node-v20.19.6-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v20.19.6/node-v20.19.6-linux-armv7l.tar.gz"],
        version = "20.19.6",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_ppc64le",
        sha256 = "7cde00025e64db108aae90c2daab301138f94a769898f3ca9ecc8b30493bf673",
        strip_prefix = "node-v20.19.6-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v20.19.6/node-v20.19.6-linux-ppc64le.tar.gz"],
        version = "20.19.6",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_s390x",
        sha256 = "fd5dc1250938ff88169f3961e66ce4e0b97d92ef3625b0972e13d4c2e408bfc7",
        strip_prefix = "node-v20.19.6-linux-s390x/",
        urls = ["https://nodejs.org/dist/v20.19.6/node-v20.19.6-linux-s390x.tar.gz"],
        version = "20.19.6",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_amd64",
        sha256 = "219a152ea859861d75adea578bdec3dce8143853c13c5187f40c40e77b0143b2",
        strip_prefix = "node-v22.21.1-linux-x64/",
        urls = ["https://nodejs.org/dist/v22.21.1/node-v22.21.1-linux-x64.tar.gz"],
        version = "22.21.1",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm64",
        sha256 = "c86830dedf77f8941faa6c5a9c863bdfdd1927a336a46943decc06a38f80bfb2",
        strip_prefix = "node-v22.21.1-linux-arm64/",
        urls = ["https://nodejs.org/dist/v22.21.1/node-v22.21.1-linux-arm64.tar.gz"],
        version = "22.21.1",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm",
        sha256 = "40d3d09aee556abc297dd782864fcc6b9e60acd438ff0660ba9ddcd569c00920",
        strip_prefix = "node-v22.21.1-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v22.21.1/node-v22.21.1-linux-armv7l.tar.gz"],
        version = "22.21.1",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_ppc64le",
        sha256 = "b24f4c19d5546cd418674e83bde56d50a7c2b65faec7a65c3502f285eeb3aa70",
        strip_prefix = "node-v22.21.1-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v22.21.1/node-v22.21.1-linux-ppc64le.tar.gz"],
        version = "22.21.1",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_s390x",
        sha256 = "7c46bd4a512b35f03acb972b2b04fecc2d4c47e35069ab9a8dd5cd8f0091195a",
        strip_prefix = "node-v22.21.1-linux-s390x/",
        urls = ["https://nodejs.org/dist/v22.21.1/node-v22.21.1-linux-s390x.tar.gz"],
        version = "22.21.1",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_amd64",
        sha256 = "6159227e0af7d7c3c6bb2fa900452b04a6cb8841a702a79acc613209d70b04d0",
        strip_prefix = "node-v24.12.0-linux-x64/",
        urls = ["https://nodejs.org/dist/v24.12.0/node-v24.12.0-linux-x64.tar.gz"],
        version = "24.12.0",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_arm64",
        sha256 = "9b2a2eeb98a8eb37361224e2a1d060300ad2dd143af58dfdb16de785df0f1228",
        strip_prefix = "node-v24.12.0-linux-arm64/",
        urls = ["https://nodejs.org/dist/v24.12.0/node-v24.12.0-linux-arm64.tar.gz"],
        version = "24.12.0",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_ppc64le",
        sha256 = "66ec79b4d64f4109aedf822108715d0b6097118df9159c2f6321477da4ea17aa",
        strip_prefix = "node-v24.12.0-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v24.12.0/node-v24.12.0-linux-ppc64le.tar.gz"],
        version = "24.12.0",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_s390x",
        sha256 = "8dc960a2555db1aa77fd131c25be571b9f7844bc8b278e78732b9f580fe7d580",
        strip_prefix = "node-v24.12.0-linux-s390x/",
        urls = ["https://nodejs.org/dist/v24.12.0/node-v24.12.0-linux-s390x.tar.gz"],
        version = "24.12.0",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = [
            "nodejs20_amd64",
            "nodejs20_arm64",
            "nodejs20_arm",
            "nodejs20_ppc64le",
            "nodejs20_s390x",
            "nodejs22_amd64",
            "nodejs22_arm64",
            "nodejs22_arm",
            "nodejs22_ppc64le",
            "nodejs22_s390x",
            "nodejs24_amd64",
            "nodejs24_arm64",
            "nodejs24_ppc64le",
            "nodejs24_s390x",
        ],
        root_module_direct_dev_deps = [],
    )

_archive = tag_class(attrs = {})

node = module_extension(
    implementation = _node_impl,
    tag_classes = {
        "archive": _archive,
    },
)
