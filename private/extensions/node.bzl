"node"

BUILD_TMPL = """\
# GENERATED BY node_archive.bzl
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        [
            "output/bin/node",
            "output/LICENSE",
        ],
    ),
    package_dir = "/nodejs",
    strip_prefix = "external/{canonical_name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    rctx.report_progress("Fetching {}".format(rctx.attr.package_name))
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = rctx.attr.type,
        stripPrefix = rctx.attr.strip_prefix,
        output = "output",
    )
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
            "{{SHA256}}": rctx.attr.sha256,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            canonical_name = rctx.attr.name,
            name = rctx.attr.name.split("~")[-1],
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

node_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".tar.gz"),
        "strip_prefix": attr.string(),
        "package_name": attr.string(default = "nodejs"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        # control is only used to populate the sbom, see https://github.com/GoogleContainerTools/distroless/issues/1373
        # for why writing debian control files to the image is incompatible with scanners.
        "control": attr.label(),
    },
)

def _node_impl(module_ctx):
    mod = module_ctx.modules[0]

    if len(module_ctx.modules) > 1:
        fail("node.archive should be called only once")
    if not mod.is_root:
        fail("node.archive should be called from root module only.")

    # Node (https://nodejs.org/en/about/releases/)
    # Follow Node's maintainence schedule and support all LTS versions that are not end of life

    node_archive(
        name = "nodejs20_amd64",
        sha256 = "086ab500a98900a4c05127559b2cec4d659b3aa674453be5028d416de4eb1532",
        strip_prefix = "node-v20.19.1-linux-x64/",
        urls = ["https://nodejs.org/dist/v20.19.1/node-v20.19.1-linux-x64.tar.gz"],
        version = "20.19.1",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm64",
        sha256 = "f8fac0cc6cb06b4e443febda59178d5a6c6a5379e0194e6d156d3d3116e75b5f",
        strip_prefix = "node-v20.19.1-linux-arm64/",
        urls = ["https://nodejs.org/dist/v20.19.1/node-v20.19.1-linux-arm64.tar.gz"],
        version = "20.19.1",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm",
        sha256 = "8bcb0c923f0c6ebd0e4e635fb8765bf1a02b64a5f9a679a4fa94cd9f53e5ee74",
        strip_prefix = "node-v20.19.1-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v20.19.1/node-v20.19.1-linux-armv7l.tar.gz"],
        version = "20.19.1",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_ppc64le",
        sha256 = "e9f603f6494a453164dcd9ede175d14ea13e61838881dc0944cf230ff5d68c69",
        strip_prefix = "node-v20.19.1-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v20.19.1/node-v20.19.1-linux-ppc64le.tar.gz"],
        version = "20.19.1",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_s390x",
        sha256 = "062c526695ed64cacab20d266b0a993ce264ee3e1fe5da007a51ba98357c7321",
        strip_prefix = "node-v20.19.1-linux-s390x/",
        urls = ["https://nodejs.org/dist/v20.19.1/node-v20.19.1-linux-s390x.tar.gz"],
        version = "20.19.1",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_amd64",
        sha256 = "29d1c60c5b64ccdb0bc4e5495135e68e08a872e0ae91f45d9ec34fc135a17981",
        strip_prefix = "node-v22.15.0-linux-x64/",
        urls = ["https://nodejs.org/dist/v22.15.0/node-v22.15.0-linux-x64.tar.gz"],
        version = "22.15.0",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm64",
        sha256 = "c3582722db988ed1eaefd590b877b86aaace65f68746726c1f8c79d26e5cc7de",
        strip_prefix = "node-v22.15.0-linux-arm64/",
        urls = ["https://nodejs.org/dist/v22.15.0/node-v22.15.0-linux-arm64.tar.gz"],
        version = "22.15.0",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm",
        sha256 = "639a3ee3217049ba20e6f05651c7281a9d007111196387eb177081af8851e52f",
        strip_prefix = "node-v22.15.0-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v22.15.0/node-v22.15.0-linux-armv7l.tar.gz"],
        version = "22.15.0",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_ppc64le",
        sha256 = "a744107732546a3b112630cde1cc2db681559668cea36bee851348ed036c101c",
        strip_prefix = "node-v22.15.0-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v22.15.0/node-v22.15.0-linux-ppc64le.tar.gz"],
        version = "22.15.0",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_s390x",
        sha256 = "600afa22128e5d1b7d472109b8f9f93ace161dab3c1a8a642895675471916d86",
        strip_prefix = "node-v22.15.0-linux-s390x/",
        urls = ["https://nodejs.org/dist/v22.15.0/node-v22.15.0-linux-s390x.tar.gz"],
        version = "22.15.0",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = [
            "nodejs20_amd64",
            "nodejs20_arm64",
            "nodejs20_arm",
            "nodejs20_ppc64le",
            "nodejs20_s390x",
            "nodejs22_amd64",
            "nodejs22_arm64",
            "nodejs22_arm",
            "nodejs22_ppc64le",
            "nodejs22_s390x",
        ],
        root_module_direct_dev_deps = [],
    )

_archive = tag_class(attrs = {})

node = module_extension(
    implementation = _node_impl,
    tag_classes = {
        "archive": _archive,
    },
)
