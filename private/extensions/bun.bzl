"bun"

BUILD_TMPL = """# GENERATED BY bun.bzl
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        [
            "output/bun",
        ],
    ),
    package_dir = "/bun",
    strip_prefix = "external/{canonical_name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    rctx.report_progress("Fetching {}".format(rctx.attr.package_name))
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = rctx.attr.type,
        stripPrefix = rctx.attr.strip_prefix,
        output = "output",
    )
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
            "{{SHA256}}": rctx.attr.sha256,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            canonical_name = rctx.attr.name,
            name = rctx.attr.name.split("~")[-1],
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

bun_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".zip"),
        "strip_prefix": attr.string(),
        "package_name": attr.string(default = "bun"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        # control is only used to populate the sbom, see https://github.com/GoogleContainerTools/distroless/issues/1373
        # for why writing debian control files to the image is incompatible with scanners.
        "control": attr.label(),
    },
)

def _bun_impl(module_ctx):
    mod = module_ctx.modules[0]

    if len(module_ctx.modules) > 1:
        fail("bun.archive should be called only once")
    if not mod.is_root:
        fail("bun.archive should be called from root module only.")

    # Bun (https://github.com/oven-sh/bun/releases)
    # Follow Node's maintenance schedule and support all LTS versions that are not end of life

    bun_archive (
        name = "bun_amd64",
        sha256 = "f5c546736f955141459de231167b6fdf7b01418e8be3609f2cde9dfe46a93a3d",
        strip_prefix = "bun-linux-x64/",
        urls = ["https://github.com/oven-sh/bun/releases/download/bun-v1.3.3/bun-linux-x64.zip"],
        version = "1.3.3",
        architecture = "amd64",
        control = "//bun:control",
    )

    bun_archive (
        name = "bun_arm64",
        sha256 = "41b9f4f25256db897c2c135320e4f96c373e20ae6f06d8015187dac83591efc8",
        strip_prefix = "bun-linux-aarch64/",
        urls = ["https://github.com/oven-sh/bun/releases/download/bun-v1.3.3/bun-linux-aarch64.zip"],
        version = "1.3.3",
        architecture = "arm64",
        control = "//bun:control",
    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = [
            "bun_amd64",
            "bun_arm64",
        ],
        root_module_direct_dev_deps = [],
    )

_archive = tag_class(attrs = {})

bun = module_extension(
    implementation = _bun_impl,
    tag_classes = {
        "archive": _archive,
    },
)
